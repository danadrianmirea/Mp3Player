apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco'
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: 'com.github.ben-manes.versions'


jacoco {
    toolVersion = "$JACOCO_VERSION"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReportFullDebug(type: JacocoReport, dependsOn: ['testFullDebugUnitTest']) {

    println "reporting for duty"
    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [
            '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/*$[0-9].*'
    ]

    def mainSrc = "$project.projectDir/src/main/java"
    def testSupportSrc = "$projectDir/testsupport/src/main/java"
    def daggerSrc = "$project.buildDir/generated/source/kapt/fullDebug"
    def testSupportDaggerSrc = "$projectDir/testsupport/build/generated/source/kapt/fullDebug"

    def mainClasses = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/fullDebugUnitTest", excludes: fileFilter)
    def daggerClasses = fileTree(dir: "$project.buildDir/build/intermediates/javac/fullDebugUnitTest/classes", excludes: fileFilter)
    def testSupportClasses = fileTree(dir: "$projectDir/testsupport/build/tmp/kotlin-classes/fullDebugUnitTest", excludes: fileFilter)
    def testSupportDaggerClasses = fileTree(dir: "$projectDir/testsupport/build/intermediates/javac/fullDebugUnitTest/classes", excludes: fileFilter)


    sourceDirectories.from = files([mainSrc, testSupportSrc, daggerSrc, testSupportDaggerSrc])
    classDirectories.from = files([mainClasses, daggerClasses, testSupportClasses, testSupportDaggerClasses])
    executionData.from = "$project.buildDir/jacoco/testFullDebugUnitTest.exec"
}

android {
    compileSdkVersion TARGET_SDK_VERION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERION
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            testCoverageEnabled = true
        }
    }

    flavorDimensions 'default'
    productFlavors {
        full {
            dimension = 'default'
        }
        automation {
            dimension = 'default'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
            jacoco {
                includeNoLocationClasses = true
                includes = ['com/github/goldy1992/mp3player/server/**']
                excludes =  [
                        '**/dagger/**',
                        '**/commons/**',
                        '**_Factory**',
                        '**_MembersInjector**',
                        '**/R.class',
                        '**/R$*.class',
                        '**/BuildConfig.*',
                        '**/Manifest*.*',
                        '**/*Test*.*',
                        'android/**/*.*',
                        '**/*$[0-9].*'
                ]
            }
        }
    }

    sourceSets {
        test {
            java.srcDirs = ["src/main/java",
                            "$projectDir/testsupport/src/main/java",
                            "$project.buildDir/generated/source/kapt/fullDebug",
                            "$projectDir/testsupport/build/generated/source/kapt/fullDebug" ]
        }
        androidTest.java.srcDirs += 'src/androidTest/java'
    }
}

dependencies {

    //dagger
    kapt group: 'com.google.dagger', name: 'dagger-compiler', version: DAGGER_VERSION
    api group: 'com.google.dagger', name: 'dagger', version: DAGGER_VERSION

    implementation group: 'androidx.media', name: 'media', version: MEDIA_VERSION
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: COMMONS_COLLECTIONS4_VERSION
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: COMMONS_LANG_VERSION
    implementation group: 'com.google.guava', name: 'guava', version: GOOGLE_GUAVA_VERSION


    implementation group: 'androidx.room', name: 'room-runtime', version: ROOM_VERSION
    kaptTest group: 'androidx.room', name: 'room-compiler', version: ROOM_VERSION
    kapt group: 'androidx.room', name: 'room-compiler', version: ROOM_VERSION


    implementation group: 'com.google.android.exoplayer', name: 'exoplayer-core', version: EXO_PLAYER_VERSION
    implementation group: 'com.google.android.exoplayer', name: 'exoplayer-ui', version: EXO_PLAYER_VERSION
    implementation group: 'com.google.android.exoplayer', name: 'extension-mediasession', version: EXO_PLAYER_VERSION

    api project(':commons')


    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation project(path: ':commons')

    testApi project(path: ':service:testsupport')


    testImplementation junit5UnitTests
    testRuntimeOnly junit5UnitTestRuntime

    testImplementation group: 'org.robolectric', name: 'robolectric', version: ROBOLECTRIC_VERSION// https://mvnrepository.com/artifact/org.robolectric/shadows-support-v4
    testImplementation group: 'org.robolectric', name: 'shadows-supportv4', version: ROBOLECTRIC_VERSION
    testImplementation group: 'com.nhaarman.mockitokotlin2', name: 'mockito-kotlin', version: MOCKITO_KOTLIN


    implementation "androidx.core:core-ktx:1.1.0"
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
}
